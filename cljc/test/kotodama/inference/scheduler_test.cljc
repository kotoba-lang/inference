(ns kotodama.inference.scheduler-test
  "The host mirror's half of the case table `kotoba/scheduler_core.kotoba`
  asserts with `amu test`.

  Same cases, same order, and each comparison has a case ON the boundary as
  well as either side: without one, flipping `>` to `>=` leaves both suites
  green. Measured on the Kotoba side -- that mutation fails
  `test-spec-worth-it` on all three targets."
  (:require [clojure.test :refer [deftest is testing]]
            [kotodama.inference.scheduler :as s]))

(deftest kv-blocks-needed
  (is (= 0 (s/kv-blocks-needed 0 16)))
  (is (= 1 (s/kv-blocks-needed 1 16)))
  (testing "exactly one block, and one past it"
    (is (= 1 (s/kv-blocks-needed 16 16)))
    (is (= 2 (s/kv-blocks-needed 17 16))))
  (testing "a zero block size does not divide by zero"
    (is (= 0 (s/kv-blocks-needed 100 0))))
  (is (= 0 (s/kv-blocks-needed -5 16))))

(deftest kv-blocks-to-append
  (testing "appending inside a partial block is free"
    (is (= 0 (s/kv-blocks-to-append 4 4 16))))
  (is (= 1 (s/kv-blocks-to-append 12 8 16)))
  (testing "the first token of a new block costs one"
    (is (= 1 (s/kv-blocks-to-append 16 1 16))))
  (is (= 0 (s/kv-blocks-to-append 5 0 16)))
  (is (= 2 (s/kv-blocks-to-append 0 32 16))))

(deftest kv-can-allocate
  (is (true? (s/kv-can-allocate? 10 5 2)))
  (testing "exactly at the watermark is admitted; one past it is not"
    (is (true? (s/kv-can-allocate? 10 8 2)))
    (is (false? (s/kv-can-allocate? 10 9 2))))
  (is (true? (s/kv-can-allocate? 3 3 0)))
  (is (false? (s/kv-can-allocate? 10 -1 2))))

(deftest kv-free-after-alloc
  (is (= 5 (s/kv-free-after-alloc 10 5)))
  (is (= 0 (s/kv-free-after-alloc 5 5)))
  (is (= 0 (s/kv-free-after-alloc 3 9))))

(deftest kv-slot-index
  (is (= 0 (s/kv-slot-index 0 0 16)))
  (testing "the last slot of a block, and one past it"
    (is (= 31 (s/kv-slot-index 1 15 16)))
    (is (= -1 (s/kv-slot-index 1 16 16))))
  (is (= -1 (s/kv-slot-index -1 0 16)))
  (is (= -1 (s/kv-slot-index 0 0 0))))

(deftest kv-append-offset
  (is (= 0 (s/kv-append-offset 0 16)))
  (testing "the last free slot, and a full block"
    (is (= 15 (s/kv-append-offset 15 16)))
    (is (= -1 (s/kv-append-offset 16 16))))
  (is (= -1 (s/kv-append-offset -1 16))))

(deftest batch-prefill-chunk
  (is (= 512 (s/batch-prefill-chunk 2048 512 512)))
  (is (= 100 (s/batch-prefill-chunk 100 512 512)))
  (is (= 256 (s/batch-prefill-chunk 2048 256 512)))
  (is (= 0 (s/batch-prefill-chunk 0 512 512)))
  (testing "a spent budget schedules nothing rather than un-scheduling"
    (is (= 0 (s/batch-prefill-chunk 2048 0 512)))))

(deftest batch-budget-after
  (is (= 0 (s/batch-budget-after 512 512)))
  (is (= 256 (s/batch-budget-after 512 256)))
  (is (= 0 (s/batch-budget-after 512 900))))

(deftest batch-admit
  (is (true? (s/batch-admit? 100 4 2 3 8)))
  (testing "exactly at max-running is refused; one below it is admitted"
    (is (false? (s/batch-admit? 100 4 2 8 8)))
    (is (true? (s/batch-admit? 100 4 2 7 8))))
  (is (false? (s/batch-admit? 5 4 2 0 8))))

(deftest batch-decode-slots
  (is (= 8 (s/batch-decode-slots 8 512)))
  (testing "the budget binds when prefill took most of it"
    (is (= 4 (s/batch-decode-slots 8 4))))
  (is (= 0 (s/batch-decode-slots 0 512))))

(deftest preempt-rank
  (testing "newest first, even against a sequence holding every block"
    (is (> (s/preempt-rank 9 0) (s/preempt-rank 8 1023))))
  (testing "ties broken by blocks held"
    (is (> (s/preempt-rank 5 10) (s/preempt-rank 5 9))))
  (is (= 0 (s/preempt-rank 0 0))))

(deftest prefix-cached-blocks
  (testing "only full blocks are shared"
    (is (= 1 (s/prefix-cached-blocks 16 16)))
    (is (= 1 (s/prefix-cached-blocks 31 16)))
    (is (= 2 (s/prefix-cached-blocks 32 16)))
    (is (= 0 (s/prefix-cached-blocks 15 16))))
  (is (= 0 (s/prefix-cached-blocks 100 0))))

(deftest prefix-tokens-to-compute
  (is (= 100 (s/prefix-tokens-to-compute 100 0 16)))
  (is (= 68 (s/prefix-tokens-to-compute 100 2 16)))
  (testing "a fully cached prompt computes nothing"
    (is (= 0 (s/prefix-tokens-to-compute 32 2 16))))
  (testing "a stale entry cannot make it negative"
    (is (= 0 (s/prefix-tokens-to-compute 32 99 16)))))

(deftest cache-hit-bp
  (is (= 10000 (s/cache-hit-bp 100 100)))
  (is (= 5000 (s/cache-hit-bp 50 100)))
  (testing "0.4%, which a truncated percent would report as 0"
    (is (= 40 (s/cache-hit-bp 4 1000))))
  (is (= 0 (s/cache-hit-bp 0 100)))
  (is (= 0 (s/cache-hit-bp 5 0))))

(deftest spec-accept-count
  (is (= 3 (s/spec-accept-count 3 5)))
  (is (= 5 (s/spec-accept-count 5 5)))
  (testing "a miscount cannot exceed the draft"
    (is (= 5 (s/spec-accept-count 9 5))))
  (is (= 0 (s/spec-accept-count -1 5))))

(deftest spec-emitted-tokens
  (testing "nothing accepted still emits the bonus token"
    (is (= 1 (s/spec-emitted-tokens 0 5))))
  (is (= 4 (s/spec-emitted-tokens 3 5)))
  (testing "all accepted, plus the bonus"
    (is (= 6 (s/spec-emitted-tokens 5 5)))
    (is (= 6 (s/spec-emitted-tokens 9 5)))))

(deftest spec-worth-it
  (is (true? (s/spec-worth-it? 6000 4000)))
  (testing "a tie is not worth it"
    (is (false? (s/spec-worth-it? 4000 4000))))
  (is (false? (s/spec-worth-it? 3000 4000))))
