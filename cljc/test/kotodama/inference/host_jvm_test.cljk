(ns kotodama.inference.host-jvm-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotodama.inference.host.jvm :as host]))

(deftest vector-summary-is-a-stable-cross-runtime-fingerprint
  (testing "empty activations are represented without infinities"
    (is (= {:count 0 :sum 0.0 :rms 0.0 :min nil :max nil :first [] :last []}
           (host/vector-summary []))))
  (testing "statistics and edge samples identify an activation vector compactly"
    (is (= {:count 4
            :sum -2.0
            :rms (Math/sqrt 7.5)
            :min -4.0
            :max 3.0
            :first [1.0 -2.0 3.0]
            :last [-2.0 3.0 -4.0]}
           (host/vector-summary [1 -2 3 -4])))))

(deftest dot-f32-has-an-explicit-float-accumulation-contract
  (is (= 0.0 (host/dot-f32 [1.0e8 1.0 -1.0e8] [1.0 1.0 1.0])))
  (is (= 1.0 (+ (* 1.0e8 1.0) (* 1.0 1.0) (* -1.0e8 1.0))))
  (is (thrown-with-msg? clojure.lang.ExceptionInfo
                        #"dot input lengths differ"
                        (host/dot-f32 [1] [1 2]))))
